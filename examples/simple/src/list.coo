application
    construct parentNode
        dom $parentNode append [+view Page [+view Page create] render]


model Item
    property title<String>

    construct title
        this set title $title


collection List
    model Item


view Item
    property model<model Item>
    property textNode<Node>

    construct m
        this set model $m

        model Item $m
            change "title" val
                dom @textNode text set $val

            destroy
                this destroy

    render
        +template "conkitty:item" apply
            param
                +model Item @model get title

            "text" node
                this set textNode $node

            "remove" node
                dom $node
                    click
                        model Item @model destroy


view Page
    property container<Node>
    property input<Node>
    property list<collection List>

    construct
        this set list
            +collection List create
                add m
                    this call addItem $m

    render
        +template "conkitty:page" apply
            "container" node
                this set container $node

            "input" node
                this set input $node

            "add" node
                dom $node
                    click
                        collection List @list add [+dom @input value get]
                        dom @input value set ""

    method addItem m
        dom @container append
            +view Item [+view Item create $m] render
