MODEL Item
    PROPERTY title <STRING>

    CONSTRUCT title
        MODEL SET title $title


COLLECTION List
    MODEL Item


VIEW Item
    PROPERTY model <MODEL Item>
    PROPERTY textNode <(Node)>

    CONSTRUCT m
        VIEW SET model $m

        MODEL Item $m
            CHANGE "title" val
                DOM @textNode TEXT $val

            DESTROY
                VIEW DESTROY

    RENDER
        +TEMPLATE Item APPLY
            PARAM title
                +MODEL Item @model GET title

            ELEMENT "text" node
                VIEW SET textNode $node

            ELEMENT "remove" node
                DOM $node
                    CLICK
                        MODEL Item @model DESTROY


VIEW Page
    PROPERTY container <(Node)>
    PROPERTY input <(Node)>
    PROPERTY list <COLLECTION List>

    CONSTRUCT
        VIEW SET list
            +COLLECTION List CREATE
                ADD m
                    VIEW CALL addItem $m

    RENDER
        +TEMPLATE Page APPLY
            ELEMENT "container" node
                VIEW SET container $node

            ELEMENT "input" node
                VIEW SET input $node

            ELEMENT "add" node
                DOM $node
                    CLICK
                        COLLECTION List @list ADD
                            +DOM @input VALUE GET
                        DOM @input VALUE SET ""

    METHOD addItem m
        DOM @container APPEND
            SET tmp
                +VIEW Item CREATE $m

            +VIEW Item $tmp RENDER


TEMPLATE Page
    ORIGIN "conkitty:page"


TEMPLATE Item title
    ORIGIN "conkitty:item"
