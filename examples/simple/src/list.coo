APPLICATION
    CONSTRUCT parentNode
        SET p
            +VIEW Page CREATE

        DOM $parentNode APPEND
            +VIEW Page $p RENDER


MODEL Item
    PROPERTY title<String>

    CONSTRUCT title
        THIS SET title $title


COLLECTION List
    MODEL Item


VIEW Item
    PROPERTY model<MODEL Item>
    PROPERTY textNode<JS val (val instanceof Node)>

    CONSTRUCT m
        THIS SET model $m

        MODEL Item $m
            CHANGE "title" val
                DOM @textNode TEXT $val

            DESTROY
                THIS DESTROY

    RENDER
        +TEMPLATE Item APPLY
            PARAM title
                +MODEL Item @model GET title

            ON "text" node
                THIS SET textNode $node

            ON "remove" node
                DOM $node
                    CLICK
                        MODEL Item @model DESTROY


VIEW Page
    PROPERTY container<Node>
    PROPERTY input<Node>
    PROPERTY list<COLLECTION List>

    CONSTRUCT
        THIS SET list
            +COLLECTION List CREATE
                ADD m
                    THIS CALL addItem $m

    RENDER
        +TEMPLATE Page APPLY
            ON "container" node
                THIS SET container $node

            ON "input" node
                THIS SET input $node

            ON "add" node
                DOM $node
                    CLICK
                        COLLECTION List @list ADD
                            +DOM @input VALUE GET

                        DOM @input VALUE SET ""

    METHOD addItem m
        DOM @container APPEND
            SET tmp
                +VIEW Item CREATE $m

            +VIEW Item $tmp RENDER

        //+VIEW Item [+VIEW Item CREATE $m] RENDER


TEMPLATE Page
    ORIGIN "conkitty:page"


TEMPLATE Item title
    ORIGIN "conkitty:item"
